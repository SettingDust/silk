import BuildConstants.authors
import BuildConstants.githubRepo
import BuildConstants.isSnapshot

plugins {
    kotlin("jvm")

    `maven-publish`
    signing
}

publishing {
    publications {
        register<MavenPublication>(project.name) {
            from(components["java"])

            this.groupId = project.group.toString()
            this.artifactId = project.name
            this.version = rootProject.version.toString()

            pom {
                name.set(project.name)
                description.set(project.description)

                developers {
                    authors.forEach {
                        developer {
                            name.set(it)
                        }
                    }
                }

                licenses {
                    license {
                        name.set("GNU General Public License 3")
                        url.set("https://www.gnu.org/licenses/gpl-3.0.txt")
                    }
                }

                url.set("https://github.com/${githubRepo}")
            }
        }
    }
}

signing {
    sign(publishing.publications)
}
